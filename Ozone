data air; 
	input ozone @@; 
	label ozone = 'Ozone Concentration'; 
	date = intnx( 'month', '31dec1954'd, _n_ ); 
	format date monyy.; 
	month = month( date ); 
	year = year( date ); 
	summer = ( 5 < month < 11 ) * ( year > 1965 ); 
	winter = ( year > 1965 ) - summer; 
	datalines; 
	2.7 2.0 3.6 5.0 6.5 6.1 5.9 5.0 6.4 7.4 8.2 3.9 
	4.1 4.5 5.5 3.8 4.8 5.6 6.3 5.9 8.7 5.3 5.7 5.7 
	3.0 3.4 4.9 4.5 4.0 5.7 6.3 7.1 8.0 5.2 5.0 4.7 
	3.7 3.1 2.5 4.0 4.1 4.6 4.4 4.2 5.1 4.6 4.4 4.0 
	2.9 2.4 4.7 5.1 4.0 7.5 7.7 6.3 5.3 5.7 4.8 2.7 
	1.7 2.0 3.4 4.0 4.3 5.0 5.5 5.0 5.4 3.8 2.4 2.0 
	2.2 2.5 2.6 3.3 2.9 4.3 4.2 4.2 3.9 3.9 2.5 2.2 
	2.4 1.9 2.1 4.5 3.3 3.4 4.1 5.7 4.8 5.0 2.8 2.9 
	1.7 3.2 2.7 3.0 3.4 3.8 5.0 4.8 4.9 3.5 2.5 2.4 
	1.6 2.3 2.5 3.1 3.5 4.5 5.7 5.0 4.6 4.8 2.1 1.4 
	2.1 2.9 2.7 4.2 3.9 4.1 4.6 5.8 4.4 6.1 3.5 1.9 
	1.8 1.9 3.7 4.4 3.8 5.6 5.7 5.1 5.6 4.8 2.5 1.5 
	1.8 2.5 2.6 1.8 3.7 3.7 4.9 5.1 3.7 5.4 3.0 1.8 
	2.1 2.6 2.8 3.2 3.5 3.5 4.9 4.2 4.7 3.7 3.2 1.8 
	2.0 1.7 2.8 3.2 4.4 3.4 3.9 5.5 3.8 3.2 2.3 2.2 
	1.3 2.3 2.7 3.3 3.7 3.0 3.8 4.7 4.6 2.9 1.7 1.3 
	1.8 2.0 2.2 3.0 2.4 3.5 3.5 3.3 2.7 2.5 1.6 1.2 
	1.5 2.0 3.1 3.0 3.5 3.4 4.0 3.8 3.1 2.1 1.6 1.3
	;
RUN;

PROC SGPLOT DATA=air;
SERIES X=Date Y=ozone;
RUN;

PROC ARIMA DATA=air PLOTS;
	IDENTIFY VAR=ozone;
RUN;

/*
proc expand data=air out=airsmooth;
	convert air=MA12 / transout=(cmovave 12);
run;
*/
PROC PRINT DATA=air(firstobs=58 obs=63);
RUN;

/* create step dummy variable */
DATA air_step;
	SET air;
	STEP1960=(year>= 1960);
RUN;

PROC PRINT DATA=air_step(firstobs=55 obs=62);RUN;

proc arima data=air_step;
	where year>=1960;
	identify var=ozone;
run;

proc arima data=air_step;
	where year>=1960;
	identify var=ozone(1);
	identify var=ozone(12);   /* good fir, only seasonal component is significant */
	identify var=ozone(1,12); /* not really correct, most likely an overfit */
run;

proc arima data=air_step;
	where year>=1960;
	identify var=ozone(12); 
	estimate p=(1)(12) q=(1)(12) method=ml;
run;

proc arima data=air_step;
	where year>=1960;
	identify var=ozone(12); 
	estimate p=(1) q=(1)(12) method=ml;
run;

/*   For data set 1960 and onwards

                              AIC          SIC
SARIMA(1,0,1)(1,1,1)_12    293.2237     308.0727
SARIMA(1,0,1)(0,1,1)_12    291.4439     303.3242
*/

proc arima data=air_step;
	identify var=ozone(12) crosscorr=step1960(12); 
	estimate p=(1) q=(1)(12) input=step1960 method=ml;
run;

proc arima data=air_step;
	identify var=ozone(12) crosscorr=step1960(12); 
	estimate p=(1) q=(12) input=step1960 method=ml;
	outlier id=date;
run;

proc arima data=air_step;
	identify var=ozone(12) crosscorr=step1960(12); 
	estimate q=(1)(12) input=step1960 method=ml;
run;


/*   For full dataset with step function

                              AIC          SIC
SARIMA(1,0,1)(0,1,1)_12    506.2464     522.837
SARIMA(1,0,0)(0,1,1)_12    504.601      517.874
SARIMA(0,0,1)(0,1,1)_12    506.0351     519.3076

Final model: SARIMA(1,0,0)(0,1,1)_12 

AR 1 - 0.30121 B**(1)
MA 1 - 0.75111 B**(12)

Estimated Intercept	-0.08878
Period(s) of Differencing	12


(AR) Z_t= (MA)a_t

(1 - 0.30121 B)(1 - 0.75111 B^12) nabla^12 (Z_t + 0.08878) = (1 - 0.75111 B^12) a_t + 0.91813 nabla^12  STEP1960

*/
